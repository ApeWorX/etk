program = _{ SOI ~ "\n"* ~ (stmt ~ "\n"+)* ~ stmt? ~ EOI }

stmt = _{ expr }

expr = _{ jumpdest | op | push | inst_macro }

op = @{
	"origin" | "stop" | "mul" | "sub" | "div" | "sdiv" | "mod" | "smod" |
	"addmod" | "mulmod" | "exp" | "signextend" | "lt" | "gt" | "slt" |
	"sgt" | "eq" | "iszero" | "and" | "or" | "xor" | "not" | "shl" | "shr" |
	"sar" | "sha3" | "address" | "add" | "balance" | "caller" |
	"callvalue" | "calldataload" | "calldatasize" | "calldatacopy" |
	"codesize" | "codecopy" | "gasprice" | "extcodesize" | "extcodecopy" |
	"returndatasize" | "returndatacopy" | "extcodehash" | "blockhash" |
	"coinbase" | "timestamp" | "number" | "difficulty" | "gaslimit" |
	"pop" | "mload" | "mstore8" | "mstore" | "sload" | "sstore" | "jumpi" |
	"jump" | "pc" | "msize" | "gas" | swap | dup | log |
	"create" | "call" | "callcode" | "return" | "delegatecall" | "create2" |
	"staticcall" | "revert" | "selfdestruct"
}

push = ${ "push" ~  word_size ~ WHITESPACE ~ numeric_argument }
swap = { "swap" ~ half_word_size }
dup  = { "dup" ~ half_word_size }
log = { "log" ~ '0'..'4' }

string = @{ "\"" ~ string_char* ~ "\"" }
string_char = _{ "\\\\" | "\\\"" | (!"\\" ~ !"\"" ~ ANY) }

word_size = @{ ('1'..'2' ~ '0'..'9') | ("3" ~ '0'..'2') | '1'..'9' }
half_word_size = @{ ("1" ~ '0'..'6') | '1'..'9' }

binary = @{ "0b" ~ ASCII_BIN_DIGIT+ }
octal = @{ "0o" ~ ASCII_OCT_DIGIT+ }
decimal = @{ ASCII_DIGIT+ }
hex = @{ "0x" ~ ASCII_HEX_DIGIT ~ ASCII_HEX_DIGIT+ }
number = _{ binary | octal | hex | decimal }

selector = { "selector(\"" ~ function_declaration ~ "\")" }
function_declaration = { ASCII_ALPHANUMERIC+ ~ "(" ~ ASCII_ALPHANUMERIC* ~ ("," ~ ASCII_ALPHANUMERIC+)* ~ ")" }

label = @{ "." ~ (ASCII_ALPHA_LOWER ~ "_"*)+ }
jumpdest = { "jumpdest" ~ label }

arguments = _{ "(" ~ arguments_list? ~ ")" }
arguments_list = _{ ( argument ~ "," )* ~ argument? }
argument = _{ string | numeric_argument }
numeric_argument = _{ number | label | selector }

inst_macro = ${ "%" ~ ( import | include | include_hex | push_macro ) }

import = !{ "import" ~ arguments }
include = !{ "include" ~ arguments }
include_hex = !{ "include_hex" ~ arguments }
push_macro = !{ "push" ~ arguments }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ ";" ~ (!"\n" ~ ANY)* }
